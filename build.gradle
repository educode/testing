plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'io.freefair.lombok' version '3.8.1'
}

ext {
    junitJupiterVersion = '5.5.2'
    junitPlatformVersion = '1.5.2'
    assertjVersion = '3.12.0'
}

group 'de.hhu.educode'
version '1.0.3' + (Boolean.valueOf(System.getProperty('release')) ? '' : '-SNAPSHOT')

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenCentral()
}

dependencies {
    api "org.junit.jupiter:junit-jupiter-api:${property('junitJupiterVersion')}"
    api "org.junit.jupiter:junit-jupiter-engine:${property('junitJupiterVersion')}"
    api "org.junit.platform:junit-platform-launcher:${property('junitPlatformVersion')}"
    api "org.junit.platform:junit-platform-engine:${property('junitPlatformVersion')}"
    api "org.junit.platform:junit-platform-commons:${property('junitPlatformVersion')}"
    api "org.assertj:assertj-core:${property('assertjVersion')}"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-Werror"]
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

test {
    useJUnitPlatform()
}

apply from: 'publish.gradle'
apply from: 'bintray.gradle'

wrapper {
    gradleVersion = '5.6.2'
}
